apply plugin: 'com.android.application'

android {
    signingConfigs {
        localConfig {
            keyAlias 'androiddebugkey'
            storeFile file(System.getProperty("user.home") + '/.android/debug.keystore')
            storePassword 'android'
            keyPassword 'android'
        }
    }
    namespace 'com.arthenica.ffmpegkit.x265encoder'
    compileSdk 31
    defaultConfig {
        applicationId "com.arthenica.ffmpegkit.x265encoder"
        minSdk 24
        targetSdk 31
        versionCode 24011
        versionName "0.11"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.localConfig
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a"
            universalApk false
        }
    }
}

ext.abiCodes = ['x86': 1, 'x86_64': 2, 'armeabi-v7a': 3, 'arm64-v8a': 4]

import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each {
        output ->
            def baseAbiVersionCode = project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
            output.versionCodeOverride = baseAbiVersionCode + variant.versionCode
    }
}

dependencies {
    implementation files('../bundle-android-aar/ffmpeg-kit.aar')
    implementation 'com.arthenica:smart-exception-java:0.2.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}
